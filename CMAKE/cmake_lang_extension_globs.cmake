macro(set_enabled_languages_file_globs)
  get_property(PROJECT_ENABLED_LANGUAGES GLOBAL PROPERTY ENABLED_LANGUAGES)
  foreach(PROJECT_ENABLED_LANGUAGE ${PROJECT_ENABLED_LANGUAGES})
    list(TRANSFORM "CMAKE_${PROJECT_ENABLED_LANGUAGE}_SOURCE_FILE_EXTENSIONS"
      PREPEND "*."
      OUTPUT_VARIABLE "PROJECT_${PROJECT_ENABLED_LANGUAGE}_SOURCE_FILE_GLOB_EXPRESSIONS"
    )
    list(TRANSFORM "CMAKE_${PROJECT_ENABLED_LANGUAGE}_IGNORE_EXTENSIONS"
      PREPEND "*."
      OUTPUT_VARIABLE "PROJECT_${PROJECT_ENABLED_LANGUAGE}_IGNORE_GLOB_EXPRESSIONS"
    )
    if(PROJECT_NAME)
      set(
        ${PROJECT_NAME}_${PROJECT_ENABLED_LANGUAGE}_SOURCE_FILE_GLOB_EXPRESSIONS
        ${PROJECT_${PROJECT_ENABLED_LANGUAGE}_SOURCE_FILE_GLOB_EXPRESSIONS}
      )
      set(
        ${PROJECT_NAME}_${PROJECT_ENABLED_LANGUAGE}_IGNORE_GLOB_EXPRESSIONS
        ${PROJECT_${PROJECT_ENABLED_LANGUAGE}_IGNORE_GLOB_EXPRESSIONS}
      )
    endif(PROJECT_NAME)
  endforeach(PROJECT_ENABLED_LANGUAGE)
endmacro(set_enabled_languages_file_globs)

function(file_glob_enabled_language_files_in_directory VARIABLE_INFIX_NAME BASE_DIR)
  if(NOT VARIABLE_INFIX_NAME)
    set(VARIABLE_INFIX_NAME CURRENT_SOURCE_DIR)
  endif(NOT VARIABLE_INFIX_NAME)
  if(BASE_DIR)
    cmake_path(ABSOLUTE_PATH BASE_DIR NORMALIZE)
  else()
    set(BASE_DIR ${CMAKE_CURRENT_SOURCE_DIR})
  endif(BASE_DIR)
  unset(PROJECT_ENABLED_LANGUAGES_FILE_GLOB_VARIABLE_NAMES)
  unset(PROJECT_${VARIABLE_INFIX_NAME}_ANY_LANG_SOURCE_FILES)
  unset(PROJECT_${VARIABLE_INFIX_NAME}_ANY_LANG_IGNORES)
  get_property(PROJECT_ENABLED_LANGUAGES GLOBAL PROPERTY ENABLED_LANGUAGES)
  foreach(PROJECT_ENABLED_LANGUAGE ${PROJECT_ENABLED_LANGUAGES})
    unset("PROJECT_${VARIABLE_INFIX_NAME}_${PROJECT_ENABLED_LANGUAGE}_SOURCE_FILES")
    unset("PROJECT_${VARIABLE_INFIX_NAME}_${PROJECT_ENABLED_LANGUAGE}_IGNORES")
    list(APPEND PROJECT_ENABLED_LANGUAGES_FILE_GLOB_VARIABLE_NAMES
      "PROJECT_${VARIABLE_INFIX_NAME}_${PROJECT_ENABLED_LANGUAGE}_SOURCE_FILES"
      "PROJECT_${VARIABLE_INFIX_NAME}_${PROJECT_ENABLED_LANGUAGE}_IGNORES"
    )
    list(TRANSFORM PROJECT_${PROJECT_ENABLED_LANGUAGE}_SOURCE_FILE_GLOB_EXPRESSIONS
      PREPEND "${BASE_DIR}/"
      OUTPUT_VARIABLE PROJECT_${VARIABLE_INFIX_NAME}_${PROJECT_ENABLED_LANGUAGE}_SOURCE_FILE_GLOB_EXPRESSIONS
    )
    file(GLOB PROJECT_${VARIABLE_INFIX_NAME}_${PROJECT_ENABLED_LANGUAGE}_SOURCE_FILES
      RELATIVE ${BASE_DIR}
      ${PROJECT_${PROJECT_ENABLED_LANGUAGE}_SOURCE_FILE_GLOB_EXPRESSIONS}
    )
    if(PROJECT_${PROJECT_ENABLED_LANGUAGE}_IGNORE_GLOB_EXPRESSIONS)
      list(TRANSFORM PROJECT_${PROJECT_ENABLED_LANGUAGE}_IGNORE_GLOB_EXPRESSIONS
        PREPEND "${BASE_DIR}/"
        OUTPUT_VARIABLE PROJECT_${VARIABLE_INFIX_NAME}_${PROJECT_ENABLED_LANGUAGE}_IGNORE_GLOB_EXPRESSIONS
      )
      file(GLOB PROJECT_${VARIABLE_INFIX_NAME}_${PROJECT_ENABLED_LANGUAGE}_IGNORES
        RELATIVE ${BASE_DIR}
        ${PROJECT_${VARIABLE_INFIX_NAME}_${PROJECT_ENABLED_LANGUAGE}_IGNORE_GLOB_EXPRESSIONS}
      )
      list(APPEND PROJECT_${VARIABLE_INFIX_NAME}_ANY_LANG_IGNORES
        ${PROJECT_${VARIABLE_INFIX_NAME}_${PROJECT_ENABLED_LANGUAGE}_IGNORES}
      )
      list(REMOVE_ITEM PROJECT_${VARIABLE_INFIX_NAME}_${PROJECT_ENABLED_LANGUAGE}_SOURCE_FILES
        ${PROJECT_${VARIABLE_INFIX_NAME}_${PROJECT_ENABLED_LANGUAGE}_IGNORES}
      )
    endif(PROJECT_${PROJECT_ENABLED_LANGUAGE}_IGNORE_GLOB_EXPRESSIONS)
    list(APPEND PROJECT_${VARIABLE_INFIX_NAME}_ANY_LANG_SOURCE_FILES
      ${PROJECT_${VARIABLE_INFIX_NAME}_${PROJECT_ENABLED_LANGUAGE}_SOURCE_FILES}
    )
  endforeach(PROJECT_ENABLED_LANGUAGE)
  list(REMOVE_DUPLICATES PROJECT_${VARIABLE_INFIX_NAME}_ANY_LANG_SOURCE_FILES)
  list(REMOVE_DUPLICATES PROJECT_${VARIABLE_INFIX_NAME}_ANY_LANG_IGNORES)
  list(REMOVE_ITEM PROJECT_${VARIABLE_INFIX_NAME}_ANY_LANG_IGNORES
    ${PROJECT_${VARIABLE_INFIX_NAME}_ANY_LANG_SOURCE_FILES}
  )
  return(PROPAGATE
    PROJECT_${VARIABLE_INFIX_NAME}_ANY_LANG_SOURCE_FILES
    PROJECT_${VARIABLE_INFIX_NAME}_ANY_LANG_IGNORES
    ${PROJECT_ENABLED_LANGUAGES_FILE_GLOB_VARIABLE_NAMES}
  )
endfunction(file_glob_enabled_language_files_in_directory)

macro(file_glob_enabled_language_files)
  file_glob_enabled_language_files_in_directory(
    CURRENT_SOURCE_DIR ""
  )
endmacro(file_glob_enabled_language_files)

macro(set_asm_masm_source_files_language_property)
  set_source_files_properties(
    ${PROJECT_CURRENT_SOURCE_DIR_ASM_MASM_SOURCE_FILES}
    PROPERTIES LANGUAGE ASM_MASM
  )
endmacro(set_asm_masm_source_files_language_property)
